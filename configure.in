dnl Process this file with autoconf to produce a configure script.

AC_INIT(gaphor, 0.1.0)

AC_CONFIG_SRCDIR([gaphor/gaphor.py])

dnl This is the only place where the package version appears
AM_INIT_AUTOMAKE(AC_PACKAGE_NAME, AC_PACKAGE_VERSION)

dnl put the ACLOCAL flags in the makefile
ACLOCAL="$ACLOCAL $ACLOCAL_FLAGS"

AM_MAINTAINER_MODE

FILES_FILES="gaphor/Files \
	gaphor/UML/Files \
	gaphor/diagram/Files \
	gaphor/ui/Files \
	gaphor/ui/command/Files \
	gaphor/misc/Files"

AC_SUBST(FILES_FILES)

dnl AC_CONFIG_HEADERS(config.h)

##########
# GetText

GETTEXT_PACKAGE=gaphor
AC_SUBST(GETTEXT_PACKAGE)
ALL_LINGUAS="nl"

AM_GAPHOR_GNU_GETTEXT

LOCALEDIR=$datadir/locale
AC_SUBST(LOCALEDIR)

#########
# Python

AM_PATH_PYTHON

AC_MSG_CHECKING(for python >= 2.2)
prog="
import sys, string
minver = (2,2,0,'final',0)
if sys.version_info < minver:
	sys.exit(1)
sys.exit(0)"
if $PYTHON -c "$prog" 1>&AC_FD_CC 2>&AC_FD_CC; then
	AC_MSG_RESULT(okay)
else
	AC_MSG_ERROR(too old)
fi

dnl AM_CHECK_PYTHON_HEADERS

m4_define(not_found_msg, This Python module is required)
AM_CHECK_PYMOD(types, , , AC_MSG_ERROR(not_found_msg))
AM_CHECK_PYMOD(sys, , , AC_MSG_ERROR(not_found_msg))
dnl AM_CHECK_PYMOD(libxml2, , , AC_MSG_ERROR(not_found_msg))
dnl xmllib is still used by utils/genUML.py
AM_CHECK_PYMOD(xmllib, , , AC_MSG_ERROR(not_found_msg))
AM_CHECK_PYMOD(copy, , , AC_MSG_ERROR(not_found_msg))
AM_CHECK_PYMOD(gobject, , , AC_MSG_ERROR(not_found_msg))
AM_CHECK_PYMOD(gtk, , , AC_MSG_ERROR(not_found_msg))
AM_CHECK_PYMOD(gnome, , , AC_MSG_ERROR(not_found_msg))
dnl AM_CHECK_PYMOD(gnome.ui, , , AC_MSG_ERROR(not_found_msg))
AM_CHECK_PYMOD(gnome.canvas, , , AC_MSG_ERROR(not_found_msg))
AM_CHECK_PYMOD(diacanvas, , , AC_MSG_ERROR(not_found_msg))

AC_MSG_CHECKING(for GTK+ >= 2.0.0)
prog="
import sys, gtk
minver = (2,0,0)
if gtk.gtk_version < minver:
	sys.exit(1)
sys.exit(0)"
if $PYTHON -c "$prog" 1>&AC_FD_CC 2>&AC_FD_CC; then
	AC_MSG_RESULT(okay)
else
	AC_MSG_ERROR(too old)
fi

AC_MSG_CHECKING(for PyGTK >= 1.99.12)
prog="
import sys, gtk
minver = (1,99,12)
print gtk.pygtk_version,
if gtk.pygtk_version < minver:
	sys.exit(1)
sys.exit(0)"
if $PYTHON -c "$prog" 1>&AC_FD_CC 2>&AC_FD_CC; then
	AC_MSG_RESULT(okay)
else
	AC_MSG_ERROR(too old)
fi

AC_MSG_CHECKING(for DiaCanvas2 >= 0.8.0)
prog="
import sys, diacanvas
minver = (0,8,0)
print diacanvas.diacanvas_version,
if diacanvas.diacanvas_version < minver:
	sys.exit(1)
sys.exit(0)"
if $PYTHON -c "$prog" 1>&AC_FD_CC 2>&AC_FD_CC; then
	AC_MSG_RESULT(okay)
else
	AC_MSG_ERROR(too old)
fi

#########
# Output

DIRS=""
FILES=""
for FILE in $FILES_FILES
do
	AC_MSG_NOTICE([Generating file list from file $FILE])
	DIR=`dirname "$FILE"`
	DIRS="$DIRS $DIR"
	for ENTRY in `cat "$FILE" | sed 's/^#.*//g'`
	do
		FILES="$FILES $DIR/$ENTRY"
	done
done

AC_SUBST(DIRS)
AC_SUBST(FILES)


# Set up some default paths
GAPHOR_DATADIR=`eval echo "$datadir/$PACKAGE"`
GAPHOR_LOCALEDIR=`eval echo "$LOCALEDIR"`

AC_SUBST(GAPHOR_DATADIR)
AC_SUBST(GAPHOR_LOCALEDIR)

AC_CONFIG_FILES([bin/gaphor], [chmod +x bin/gaphor])
AC_CONFIG_FILES(
	Makefile
	data/Makefile
	data/pixmaps/Makefile
	utils/Makefile
	gaphor/config.py
	po/Makefile.in
	tests/Makefile
	doc/Makefile)
AC_OUTPUT

echo "
Gaphor is configured:

	Source code location:	$srcdir
	Python interpreter:	$PYTHON

NOTE:	You do not need to install Gaphor, just execute './gaphor.sh'
	in the current directory.

	Now type 'make' to build gaphor.
"
